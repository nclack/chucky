{
  description = "Development environment for chucky";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    claude-code.url = "github:sadjow/claude-code-nix";
    claude-code.inputs.nixpkgs.follows = "nixpkgs";
    claude-code.inputs.flake-utils.follows = "flake-utils";
    git-hooks.url = "github:cachix/git-hooks.nix";
    git-hooks.inputs.nixpkgs.follows = "nixpkgs";
  };

  outputs =
    {
      self, # required even if the lsp complains
      nixpkgs,
      flake-utils,
      claude-code,
      git-hooks,
    }:
    flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = import nixpkgs {
          inherit system;
          config.allowUnfree = true;
        };
        pre-commit-check = git-hooks.lib.${system}.run {
          src = ./.;
          hooks = {
            clang-format = {
              enable = true;
              types_or = pkgs.lib.mkForce [
                "c"
                "c++"
                "cuda"
              ];
            };
            gersemi = {
              enable = true;
              name = "gersemi";
              entry = "${pkgs.gersemi}/bin/gersemi -i";
              files = "(\\.cmake$|CMakeLists\\.txt$)";
              pass_filenames = true;
            };
          };
        };
      in
      {
        checks = {
          inherit pre-commit-check;
        };

        formatter = pkgs.nixfmt-tree;

        devShells.default = pkgs.mkShell.override { stdenv = pkgs.clangStdenv; } {
          name = "chucky";
          inherit (pre-commit-check) shellHook;

          nativeBuildInputs = with pkgs; [
            cmake
            claude-code.packages.${system}.default
            docker
            gdb
            gh
            man-pages
            man-pages-posix
            neocmakelsp
            ninja
            nixd
            llvmPackages.llvm   # llvm-profdata, llvm-cov for coverage
            perf
            pkg-config
            tokei
            awscli2
            python3
            uv
          ];

          buildInputs = with pkgs; [
            cudaPackages.cudatoolkit
            cudaPackages.nvcomp
            cudaPackages.nvcomp.static
            llvmPackages.openmp
            (lz4.overrideAttrs (old: {
              cmakeFlags = (old.cmakeFlags or [ ]) ++ [ "-DBUILD_STATIC_LIBS=ON" ];
            }))
            (zstd.override { enableStatic = true; })
            # s3 writer
            aws-c-common
            aws-c-cal
            aws-c-io
            aws-c-http
            aws-c-auth
            aws-c-s3
            aws-c-compression
            aws-c-sdkutils
            aws-checksums
            s2n-tls
          ];
        };
      }
    );
}
